import os

Import('env cachegen headergen validategen', 'embedManifest')

aqsisdll_sources = Split("""
	attributes.cpp
	blobby.cpp
	bound.cpp
	bucket.cpp
	bunny.cpp
	condition.cpp
	converter.cpp
	csgtree.cpp
	cubiccurves.cpp
	curves.cpp
	environment.cpp
	filters.cpp
	genpoly.cpp
	graphicsstate.cpp
	imagebuffer.cpp
	imagepixel.cpp
	imagers.cpp
	inlineparse.cpp
	lath.cpp
	lights.cpp
	linearcurves.cpp
	marchingcubes.cpp
	memorysentry.cpp
	micropolygon.cpp
	mipmap.cpp
	mpdump.cpp
	nurbs.cpp
	occlusion.cpp
	options.cpp
	parameters.cpp
	patch.cpp
	points.cpp
	polygon.cpp
	procedural.cpp
	quadrics.cpp
	render.cpp
	renderer.cpp
	ri.cpp
	shaders.cpp
	shadowmap.cpp
	stats.cpp
	subdivision2.cpp
	surface.cpp
	symbols.cpp
	teapot.cpp
	texturemap.cpp
	tifffile.cpp
	tilearray.cpp
	transform.cpp
	trimcurve.cpp""")


aqsisdll_headers = Split("""
	attributes.h
	bilinear.h
	blobby.h
	bound.h
	bucket.h
	bunny.h
	clippingvolume.h
	condition.h
	converter.h
	csgtree.h
	curves.h
	focus.h
	forwarddiff.h
	genpoly.h
	graphicsstate.h
	iattributes.h
	iddmanager.h
	idsoshadeops.h
	ilightsource.h
	imagebuffer.h
	imagepixel.h
	imagers.h
	inlineparse.h
	ioptions.h
	iraytrace.h
	irenderer.h
	ishader.h
	ishaderdata.h
	ishaderexecenv.h
	isurface.h
	itexturemap.h
	itransform.h
	kdtree.h
	lath.h
	lights.h
	lookuptable.h
	marchingcubes.h
	memorysentry.h
	micropolygon.h
	mipmap.h
	motion.h
	mpdump.h
	nurbs.h
	objectinstance.h
	occlusion.h
	options.h
	parameters.h
	patch.h
	plane.h
	points.h
	polygon.h
	procedural.h
	quadrics.h
	render.h
	renderer.h
	ri.h
	ri_cache.h
	ri_debug.h
	rifile.h
	shadeop.h
	shaders.h
	stats.h
	subdivision2.h
	surface.h
	symbols.h
	teapot.h
	texturemap.h
	tifffile.h
	tilearray.h
	transform.h
	trimcurve.h""")

aqsis_install_headers = Split('''
	ri.h
	shadeop.h
	''')

aqsisenv = env.Copy()
aqsisenv.AppendUnique(CPPDEFINES=['RI_EXPORTS', 'RIB_EXPORTS'])
aqsisenv.AppendUnique(CPPPATH=['$tiff_include_path', '$zlib_include_path'])


if env.has_key('libaqsis_add_sources'):
	aqsisdll_sources = aqsisdll_sources + ['$libaqsis_add_sources']


aqsisenv.Replace(LIBS = ['shadervm', 'aqsistypes', '$tiff_lib', '$tiffxx_lib', '$z_lib'])

aqsisenv.UseTargetOptions('libaqsis')

Import('ddmanager_objs raytrace_objs rib2_objs rib2ri_sources')

# compile rib2ri here to catch aqsisenv #defines
rib2ri_objs = aqsisenv.SharedObject('rib2ri', rib2ri_sources)

aqsisdll = aqsisenv.SharedLibrary('aqsis', [aqsisdll_sources, ddmanager_objs, raytrace_objs, rib2ri_objs, rib2_objs])

embedManifest(env, aqsisenv, aqsisdll, 'DLL')

aqsisenv.PostBuildSharedLibrary(aqsisenv, aqsisdll)

Depends(aqsisdll, cachegen)
Depends(aqsisdll, validategen)
Depends(aqsisdll, headergen)

aqsisInstall = aqsisenv.InstallAs([os.path.join('${RENDERENGINEDIR}', '%s${SHLIB_VERSION_SUFFIX}') %(aqsisdll[0])], [aqsisdll[0]])
aqsisenv.Install('$INCLUDEDIR', aqsis_install_headers)

aqsisenv.PostInstallSharedLibrary(aqsisenv, '${RENDERENGINEDIR}', aqsisdll)

#aqsisproject = aqsisenv.MSVSProject(target = 'render' + env['MSVSPROJECTSUFFIX'],
#	srcs = aqsisdll_sources,
#	buildtarget = aqsisdll,
#	variant = 'Release')

#aqsisenv.Depends(aqsisdll, aqsisproject)


#-------------------------------------------------------------------------------
# Unit Tests
Import('testEnv')
testEnv = testEnv.Copy()
testEnv.AppendUnique(LIBS=['aqsis'])
aqsisdll_test_sources = Split('''
	tifffile_test.cpp
	mipmap_test.cpp
	''')
testEnv.addUnitTest('aqsisdll_test', aqsisdll_test_sources)


env.Distribute(aqsisdll_sources)
env.Distribute(aqsisdll_test_sources)
env.Distribute(aqsisdll_headers)
env.Distribute('SConscript')
